
USING 'natives.sch'


SCRIPT startup

    _SET_PLAYER_IS_IN_DIRECTOR_MODE(FALSE)
    _SET_PLAYER_IS_IN_ANIMAL_FORM(FALSE)

    SET_INSTANCE_PRIORITY_MODE(0)

    REGISTER_SCRIPT_BRAINS()

    REQUEST_MODEL(`csb_prolsec`)
    WHILE NOT HAS_MODEL_LOADED(`csb_prolsec`)
        WAIT(0)
    ENDWHILE

    SET_PLAYER_MODEL(PLAYER_ID(), `csb_prolsec`)
    IF NOT IS_ENTITY_DEAD(PLAYER_PED_ID(), FALSE)
        SET_ENTITY_COORDS(PLAYER_PED_ID(), 0.0, 0.0, 75.0, TRUE, FALSE, FALSE, TRUE)
        SET_ENTITY_HEADING(PLAYER_PED_ID(), 180.0)
    ENDIF

    NEW_LOAD_SCENE_START_SPHERE(0.0, 0.0, 75.0, 10.0, 0)
    INT maxLoadTime = GET_GAME_TIMER() + 10000
    WHILE NOT IS_NEW_LOAD_SCENE_LOADED() AND GET_GAME_TIMER() < maxLoadTime
        WAIT(0)
    ENDWHILE

    SHUTDOWN_LOADING_SCREEN()
    DO_SCREEN_FADE_IN(2500)

    NEW_LOAD_SCENE_STOP()

    LOAD_AND_START_SCRIPT(`trainer_menu`, 1424)
    SET_SCRIPT_WITH_NAME_HASH_AS_NO_LONGER_NEEDED(`trainer_menu`)

    WHILE TRUE
        IF NOT IS_SCREEN_FADED_IN()
            DO_SCREEN_FADE_IN(0)
        ENDIF

        SET_TEXT_SCALE(0.3, 0.3)
        SET_TEXT_CENTRE(TRUE)
        DRAW_TEXT("CUSTOM STARTUP", 0.5, 0.1)

        WAIT(0)
    ENDWHILE
ENDSCRIPT

PROC DRAW_TEXT(STRING text, FLOAT x, FLOAT y)
    BEGIN_TEXT_COMMAND_DISPLAY_TEXT("STRING")
    ADD_TEXT_COMPONENT_SUBSTRING_PLAYER_NAME(text)
    END_TEXT_COMMAND_DISPLAY_TEXT(x, y, 0)
ENDPROC

PROC LOAD_AND_START_SCRIPT(INT nameHash, INT stackSize)
	REQUEST_SCRIPT_WITH_NAME_HASH(nameHash)
	WHILE NOT HAS_SCRIPT_WITH_NAME_HASH_LOADED(nameHash)
		WAIT(0)
		REQUEST_SCRIPT_WITH_NAME_HASH(nameHash)
	ENDWHILE
	START_NEW_SCRIPT_WITH_NAME_HASH(nameHash, stackSize)
ENDPROC

PROC REGISTER_SCRIPT_BRAINS()
    REGISTER_OBJECT_SCRIPT_BRAIN("ob_vend", `prop_vend_soda_01`, 100, 10.0, -1, 9)
    REGISTER_OBJECT_SCRIPT_BRAIN("ob_vend", `prop_vend_soda_02`, 100, 10.0, -1, 9)
    REGISTER_OBJECT_SCRIPT_BRAIN("ob_vend", `prop_vend_snak_01`, 100, 10.0, -1, 9)

    ENABLE_SCRIPT_BRAIN_SET(2)
    ENABLE_SCRIPT_BRAIN_SET(4)
    ENABLE_SCRIPT_BRAIN_SET(8)
    ENABLE_SCRIPT_BRAIN_SET(1)
ENDPROC
